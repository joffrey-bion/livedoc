plugins {
    id 'org.springframework.boot' version '1.5.12.RELEASE'
    id 'com.palantir.docker' version '0.19.2'
}

apply plugin: 'java'
apply plugin: 'com.palantir.docker'

group = 'org.example.shelf'

dependencies {
    // to enable documentation from Javadoc
    annotationProcessor project(':livedoc-javadoc-processor')

    compile project(':livedoc-springboot')
    compile project(':livedoc-ui-webjar')
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-websocket'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-security'
    // required by spring security with websockets
    compile 'org.springframework.security:spring-security-messaging'
    compile 'com.h2database:h2:1.4.196'
}

springBoot {
    buildInfo()
}

docker {
    name "${rootProject.dockerUser}/livedoc-demo"
    tags 'latest', "${project.version}"
    files jar.archivePath
    buildArgs(['JAR_FILE': "${jar.archiveName}"])
}
tasks.docker.dependsOn(tasks.build)
tasks.dockerPush.dependsOn { tasks.dockerTag }

rootProject.tasks.release.dependsOn(tasks.dockerPush)
