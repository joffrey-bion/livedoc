plugins {
    id "com.moowork.node" version "1.2.0"
}

apply plugin: 'base'

description = 'The Livedoc UI packaged in a webjar'
buildDir = 'build'

node {
    version = '10.8.0'
    yarnVersion = '1.9.4'
    download = true
}

project.afterEvaluate {
    // install task outputs seem incorrect
    tasks.yarn_install.outputs.dir(file('node_modules'))
}

task bundle(type: YarnTask) {
    inputs.dir(new File('public'))
    inputs.dir(new File('src'))
    outputs.dir(new File("$buildDir"))
    args = ['run', 'build']
}

task start(type: YarnTask) {
    args = ['start']
}

task testJs(type: YarnTask) {
    inputs.dir(file('src'))
    outputs.files(file("${buildDir}/reports/yarn-tests-run.out")) // fake output to avoid re-run
    args = ['test:oneshot']
}

task syncPackageVersion(type: YarnTask) {
    args = ['version', '--no-git-tag-version', '--new-version', project.version]
}
bundle.mustRunAfter(syncPackageVersion)
rootProject.tasks.syncVersion.dependsOn(syncPackageVersion)

jar {
    from('build') {
        include '*.*'
        include 'static/**/*'
        into 'META-INF/resources/livedoc'
    }
}

check.dependsOn(testJs)
bundle.dependsOn(yarn_install)
jar.dependsOn(bundle)
