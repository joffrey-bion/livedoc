plugins {
    id "com.moowork.node" version "1.2.0"
}

apply plugin: 'base'

description = 'The Livedoc UI packaged in a webjar'
buildDir = 'build'

node {
    version = '9.7.1'
    yarnVersion = '1.5.1'
    download = true
}

task bundle(type: YarnTask) {
    inputs.dir(new File('public'))
    inputs.dir(new File('src'))
    outputs.dir(new File("$buildDir"))
    args = ['run', 'build']
}

task start(type: YarnTask) {
    args = ['start']
}

task testJs(type: YarnTask) {
    args = ['test:oneshot']
}

task syncPackageVersion(type: YarnTask) {
    args = ['version', '--new-version', project.version]
}
bundle.mustRunAfter(syncPackageVersion)
rootProject.tasks.syncVersion.dependsOn(syncPackageVersion)

jar {
    from('build') {
        include '*.*'
        include 'static/**/*'
        into 'META-INF/resources/livedoc'
    }
}

check.dependsOn(testJs)
bundle.dependsOn(yarn_install)
jar.dependsOn(bundle)
