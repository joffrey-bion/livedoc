plugins {
    id "com.github.node-gradle.node" version "3.0.1"
}

apply plugin: 'java-library'

description = 'The Livedoc UI packaged in a webjar'
buildDir = 'build'

node {
    download = true
    version = '14.16.0'
}

project.afterEvaluate {
    // install task outputs seem incorrect
    tasks.yarn.outputs.dir(file('node_modules'))
}

task bundle(type: YarnTask) {
    inputs.dir(new File('public'))
    inputs.dir(new File('src'))
    outputs.dir(new File("$buildDir"))
    args = ['run', 'build']
}

task start(type: YarnTask) {
    args = ['start']
}

task testJs(type: YarnTask) {
    inputs.dir(file('src'))
    outputs.files(file("${buildDir}/reports/yarn-tests-run.out")) // fake output to avoid re-run
    args = ['test:oneshot']
}

task syncPackageVersion(type: YarnTask) {
    args = ['version', '--no-git-tag-version', '--new-version', project.version]
}
bundle.mustRunAfter(syncPackageVersion)
rootProject.tasks.syncVersion.dependsOn(syncPackageVersion)

jar {
    from('build') {
        include '*.*'
        include 'static/**/*'
        into 'META-INF/resources/livedoc'
    }
}

check.dependsOn(testJs)
bundle.dependsOn(yarn)
jar.dependsOn(bundle)
